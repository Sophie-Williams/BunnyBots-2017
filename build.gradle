buildscript {
    ext.kotlin_version = "1.2.10"

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:2018.01.11"
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "jaci.openrio.gradle.GradleRIO"
tasks.whenTaskAdded { task ->
    if (task.name == "deploy") task.dependsOn 'assemble'
}
check.dependsOn "ktlint"

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "com.github.sertain:sertain:c0ee82eb69"

    compile wpilib()
    compile "openrio.mirror.third.kauailabs:navx-java:3.0.346"
    compile ctre()
    compile "openrio.mirror.third.ctre:CTRE-phoenix-java:5.1.2.1"
    compile pathfinder()

    ktlint 'com.github.shyiko:ktlint:0.14.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

deploy {
    targets {
        target('roborio', jaci.openrio.gradle.frc.RoboRIO) {
            team = 2521
            addresses << '10.25.21.89'
        }
    }

    artifacts {
        artifact('main', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << 'roborio'

            debug = false
        }
    }
}

//wpi {
//    wpilibVersion = '...'
//    ntcoreVersion = '...'
//    opencvVersion = '...'
//    cscoreVersion = '...'
//    wpiutilVersion = '...'
//
////    ctreVersion = '...'
////    navxVersion = '...'
//
//    smartDashboardVersion = '...'
//    shuffleboardVersion = '...'
//
//    toolchainVersion = '...'
//}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest('org.sert2521.bunnybots.Robot')
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
